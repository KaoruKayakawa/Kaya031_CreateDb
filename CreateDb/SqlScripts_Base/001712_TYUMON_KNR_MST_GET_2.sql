USE [#{-BASE_DB-}#]
GO

IF  EXISTS (SELECT * FROM sys.objects WHERE object_id = OBJECT_ID(N'[dbo].[TYUMON_KNR_MST_GET_2]') AND type in (N'P'))
DROP PROCEDURE [dbo].[TYUMON_KNR_MST_GET_2]
GO

SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO

-- ====================================================
-- 名称			: TYUMON_KNR_MST_GET_2
-- 機能			: 入力一時テーブル [#wt_TYUMON_KNR_MST_GET_2_1] は、次の項目を持つ。
--				: ・CSO_KCD, CSO_TENCD, CSO_SCD, CSO_KIKAKUCD, CSO_TEKIYOYMD, CSO_STR, CSO_END, CSO_YDELKBN, CSO_MDELKBN
--				: 現状ＤＢの CSO_KCD, CSO_TENCD, CSO_SCD が一致するレコードを対象に合成を行い、結果から有効レコードのみを取得する。
--				: ・STKF_KCD, STKF_HTCD, STKF_SCD, STKF_KIKAKUCD, STKF_TEKIYOYMD, STKF_STR, STKF_END, STKF_TEKIYOYMD_END
-- 引き数		: 
-- 戻り値		: 
-- 作成日		: 2022/06/29  作成者 : 茅川
-- ====================================================
CREATE PROCEDURE [dbo].[TYUMON_KNR_MST_GET_2]
AS
BEGIN
	SET NOCOUNT ON;

	DECLARE @ErrMessage nvarchar(4000), @ErrSeverity int, @ErrState int;

	BEGIN TRY
		WITH
			t1 AS (
				SELECT STKF_KCD, STKF_HTCD, STKF_SCD, STKF_KIKAKUCD, STKF_TEKIYOYMD, STKF_STR, STKF_END, STKF_DELFG
				FROM TYUMON_KNR_MST_now
				WHERE STKF_DELFG <> 1
			),
			t2 AS (
				SELECT DISTINCT CSO_KCD, CSO_TENCD, CSO_SCD
				FROM #wt_TYUMON_KNR_MST_GET_2_1
			)
		SELECT t1.STKF_KCD, t1.STKF_HTCD, t1.STKF_SCD, t1.STKF_KIKAKUCD, t1.STKF_TEKIYOYMD, t1.STKF_STR, t1.STKF_END, t1.STKF_DELFG
		INTO #wt_TYUMON_KNR_MST_GET_2_2
		FROM t1
		INNER JOIN t2
		ON t1.STKF_KCD = t2.CSO_KCD
			AND t1.STKF_HTCD = t2.CSO_TENCD
			AND t1.STKF_SCD = t2.CSO_SCD;

		-- 予約削除（1）
		WITH
			t2 AS (
				SELECT CSO_KCD, CSO_TENCD, CSO_SCD, CSO_KIKAKUCD, CSO_TEKIYOYMD
				FROM #wt_TYUMON_KNR_MST_GET_2_1
				WHERE CSO_YDELKBN = 1
			)
		DELETE t1
		FROM #wt_TYUMON_KNR_MST_GET_2_2 t1
		INNER JOIN t2
		ON t1.STKF_KCD = t2.CSO_KCD
			AND t1.STKF_HTCD = t2.CSO_TENCD
			AND t1.STKF_SCD = t2.CSO_SCD
			AND t1.STKF_KIKAKUCD = t2.CSO_KIKAKUCD
			AND t1.STKF_TEKIYOYMD = t2.CSO_TEKIYOYMD;

		-- 予約削除（2）
		WITH
			t2 AS (
				SELECT DISTINCT CSO_KCD, CSO_TENCD, CSO_SCD, CSO_TEKIYOYMD
				FROM #wt_TYUMON_KNR_MST_GET_2_1
				WHERE CSO_YDELKBN = 2
			)
		DELETE t1
		FROM #wt_TYUMON_KNR_MST_GET_2_2 t1
		INNER JOIN t2
		ON t1.STKF_KCD = t2.CSO_KCD
			AND t1.STKF_HTCD = t2.CSO_TENCD
			AND t1.STKF_SCD = t2.CSO_SCD
			AND t1.STKF_TEKIYOYMD >= t2.CSO_TEKIYOYMD;

		-- マスタ削除（1）
		SELECT CSO_KCD, CSO_TENCD, CSO_SCD, CSO_KIKAKUCD, CSO_TEKIYOYMD
		INTO #wt_TYUMON_KNR_MST_GET_2_11
		FROM #wt_TYUMON_KNR_MST_GET_2_1
		WHERE CSO_MDELKBN = 1;

		DELETE t1
		FROM #wt_TYUMON_KNR_MST_GET_2_2 t1
		INNER JOIN #wt_TYUMON_KNR_MST_GET_2_11 t2
		ON t1.STKF_KCD = t2.CSO_KCD
			AND t1.STKF_HTCD = t2.CSO_TENCD
			AND t1.STKF_SCD = t2.CSO_SCD
			AND t1.STKF_KIKAKUCD = t2.CSO_KIKAKUCD
			AND t1.STKF_TEKIYOYMD = t2.CSO_TEKIYOYMD;

		INSERT INTO #wt_TYUMON_KNR_MST_GET_2_2
		SELECT CSO_KCD, CSO_TENCD, CSO_SCD, CSO_KIKAKUCD, CSO_TEKIYOYMD, NULL, NULL, 2
		FROM #wt_TYUMON_KNR_MST_GET_2_11;

		-- マスタ削除（2）
		SELECT DISTINCT CSO_KCD, CSO_TENCD, CSO_SCD, CSO_TEKIYOYMD
		INTO #wt_TYUMON_KNR_MST_GET_2_12
		FROM #wt_TYUMON_KNR_MST_GET_2_1
		WHERE CSO_MDELKBN = 2;

		DELETE t1
		FROM #wt_TYUMON_KNR_MST_GET_2_2 t1
		INNER JOIN #wt_TYUMON_KNR_MST_GET_2_12 t2
		ON t1.STKF_KCD = t2.CSO_KCD
			AND t1.STKF_HTCD = t2.CSO_TENCD
			AND t1.STKF_SCD = t2.CSO_SCD
			AND t1.STKF_TEKIYOYMD = t2.CSO_TEKIYOYMD;
			
		WITH
			t1 AS (
				SELECT DISTINCT STKF_KCD, STKF_HTCD, STKF_SCD, STKF_KIKAKUCD
				FROM #wt_TYUMON_KNR_MST_GET_2_2
				WHERE STKF_DELFG = 0
			)
		INSERT INTO #wt_TYUMON_KNR_MST_GET_2_2
		SELECT t1.STKF_KCD, t1.STKF_HTCD, t1.STKF_SCD, t1.STKF_KIKAKUCD, t2.CSO_TEKIYOYMD, NULL, NULL, 2
		FROM t1
		INNER JOIN #wt_TYUMON_KNR_MST_GET_2_12 t2
		ON t1.STKF_KCD = t2.CSO_KCD
			AND t1.STKF_HTCD = t2.CSO_TENCD
			AND t1.STKF_SCD = t2.CSO_SCD;

		-- 通常
		SELECT CSO_KCD, CSO_TENCD, CSO_SCD, CSO_KIKAKUCD, CSO_TEKIYOYMD, CSO_STR, CSO_END
		INTO #wt_TYUMON_KNR_MST_GET_2_13
		FROM #wt_TYUMON_KNR_MST_GET_2_1
		WHERE CSO_YDELKBN = 0 AND CSO_MDELKBN = 0;
		
		DELETE t1
		FROM #wt_TYUMON_KNR_MST_GET_2_2 t1
		INNER JOIN #wt_TYUMON_KNR_MST_GET_2_13 t2
		ON t1.STKF_KCD = t2.CSO_KCD
			AND t1.STKF_HTCD = t2.CSO_TENCD
			AND t1.STKF_SCD = t2.CSO_SCD
			AND t1.STKF_KIKAKUCD = t2.CSO_KIKAKUCD
			AND t1.STKF_TEKIYOYMD = t2.CSO_TEKIYOYMD;

		INSERT INTO #wt_TYUMON_KNR_MST_GET_2_2
		SELECT CSO_KCD, CSO_TENCD, CSO_SCD, CSO_KIKAKUCD, CSO_TEKIYOYMD, CSO_STR, CSO_END, 0
		FROM #wt_TYUMON_KNR_MST_GET_2_13;

		-- 結果取得
		SELECT d.STKF_KCD, d.STKF_HTCD, d.STKF_SCD, d.STKF_KIKAKUCD, d.STKF_TEKIYOYMD, d.STKF_STR, d.STKF_END,
			ISNULL(e.STKF_TEKIYOYMD_END, CAST('9999/12/31 23:59:59' AS datetime)) AS STKF_TEKIYOYMD_END
		FROM (
			SELECT STKF_KCD, STKF_HTCD, STKF_SCD, STKF_KIKAKUCD, STKF_TEKIYOYMD, STKF_STR, STKF_END
			FROM #wt_TYUMON_KNR_MST_GET_2_2
			WHERE STKF_DELFG = 0) d
		LEFT OUTER JOIN (
			SELECT c.STKF_KCD, c.STKF_HTCD, c.STKF_SCD, c.STKF_KIKAKUCD, c.STKF_TEKIYOYMD, DATEADD(second, -1, MIN(c.STKF_TEKIYOYMD_END)) AS STKF_TEKIYOYMD_END
			FROM (
				SELECT a.STKF_KCD, a.STKF_HTCD, a.STKF_SCD, a.STKF_KIKAKUCD, a.STKF_TEKIYOYMD, b.STKF_TEKIYOYMD AS STKF_TEKIYOYMD_END
				FROM #wt_TYUMON_KNR_MST_GET_2_2 a
				INNER JOIN #wt_TYUMON_KNR_MST_GET_2_2 b
				ON a.STKF_KCD = b.STKF_KCD
					AND a.STKF_HTCD = b.STKF_HTCD
					AND a.STKF_SCD = b.STKF_SCD
					AND a.STKF_KIKAKUCD = b.STKF_KIKAKUCD
					AND a.STKF_TEKIYOYMD < b.STKF_TEKIYOYMD) c
			GROUP BY c.STKF_KCD, c.STKF_HTCD, c.STKF_SCD, c.STKF_KIKAKUCD, c.STKF_TEKIYOYMD) e
		ON d.STKF_KCD = e.STKF_KCD
			AND d.STKF_HTCD = e.STKF_HTCD
			AND d.STKF_SCD = e.STKF_SCD
			AND d.STKF_KIKAKUCD = e.STKF_KIKAKUCD
			AND d.STKF_TEKIYOYMD = e.STKF_TEKIYOYMD;

	END TRY
	BEGIN CATCH
		SET @ErrMessage = ERROR_MESSAGE();
        SET @ErrSeverity = ERROR_SEVERITY();
        SET @ErrState = ERROR_STATE();
  
        RAISERROR (@ErrMessage, @ErrSeverity, @ErrState );
	END CATCH
END
GO
