USE [#{-BASE_DB-}#]
GO

IF  EXISTS (SELECT * FROM sys.objects WHERE object_id = OBJECT_ID(N'[dbo].[IMPORT_MIXMGROUP]') AND type in (N'P'))
DROP PROCEDURE [dbo].[IMPORT_MIXMGROUP]
GO

SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO

-- ====================================================
-- 名称			: IMPORT_MIXMGROUP
-- 機能			: テーブル MIXMATCHGRP_MST_now に CSV データを取り込む。
-- 引き数		: 
-- 戻り値		: 
-- 作成日		: 2021/05/04  作成者 : 茅川
-- ====================================================
CREATE PROCEDURE [dbo].[IMPORT_MIXMGROUP]
AS
BEGIN
	SET NOCOUNT ON;

	DECLARE @ErrMessage nvarchar(4000), @ErrSeverity int, @ErrState int;
	DECLARE @TranCnt int;

	BEGIN TRY
		SET @TranCnt = @@TRANCOUNT;

		IF @TranCnt > 0
			SAVE TRANSACTION svpt_IMPORT_MIXMGROUP_1;
		ELSE 
			BEGIN TRANSACTION;
	END TRY
	BEGIN CATCH
		SET @ErrMessage = ERROR_MESSAGE();
        SET @ErrSeverity = ERROR_SEVERITY();
        SET @ErrState = ERROR_STATE();
  
        RAISERROR (@ErrMessage, @ErrSeverity, @ErrState);
	END CATCH

	BEGIN TRY
		DECLARE @NowDt datetime = GETDATE();
		DECLARE @Tanto varchar(100) = 'sqlserv_proc_IMPORT_MIXMGROUP';

-- [会社コード、店舗コード、ミックスマッチ番号、適用日] で予約削除
		WITH
			t1 AS (
				SELECT CMG_KCD, CMG_TENCD, CMG_MMNO, CMG_TEKIYOYMD, CMG_MMNAME, CMG_IMPORTFILE
				FROM #wt_IMPORT_MIXMGROUP_1
				WHERE CMG_YDELKBN = 1
			),
			t2 AS (
				SELECT SMGM_KCD, SMGM_HTCD, SMGM_MMNOBIG, SMGM_TEKIYOYMD
				FROM MIXMATCHGRP_MST_now
				WHERE SMGM_DELFG <> 1
			)
		SELECT t1.CMG_KCD, t1.CMG_TENCD, t1.CMG_MMNO, t1.CMG_TEKIYOYMD, t1.CMG_MMNAME, t1.CMG_IMPORTFILE, t2.SMGM_KCD AS CMG_KCD_2
		INTO #wt_IMPORT_MIXMGROUP_2
		FROM t1
		LEFT OUTER JOIN t2
		ON t1.CMG_KCD = t2.SMGM_KCD
			AND t1.CMG_TENCD = t2.SMGM_HTCD
			AND t1.CMG_MMNO = t2.SMGM_MMNOBIG
			AND t1.CMG_TEKIYOYMD = t2.SMGM_TEKIYOYMD;

  -- 削除対象レコードが存在しない削除設定を取得
		SELECT CMG_KCD, CMG_TENCD, CMG_MMNO, CMG_TEKIYOYMD, CMG_MMNAME,
			RIGHT(CMG_IMPORTFILE, LEN(CMG_IMPORTFILE) - PATINDEX('%|%', CMG_IMPORTFILE)) AS CSV_LINENO
		FROM #wt_IMPORT_MIXMGROUP_2
		WHERE CMG_KCD_2 IS NULL;
		
  -- 削除対象レコードを取得
		WITH
			t1 AS (
				SELECT *
				FROM MIXMATCHGRP_MST_now
				WHERE SMGM_DELFG <> 1
			)
		SELECT t1.*
		INTO #wt_IMPORT_MIXMGROUP_3
		FROM t1
		INNER JOIN #wt_IMPORT_MIXMGROUP_2 t2
		ON t1.SMGM_KCD = t2.CMG_KCD
			AND t1.SMGM_HTCD = t2.CMG_TENCD
			AND t1.SMGM_MMNOBIG = t2.CMG_MMNO
			AND t1.SMGM_TEKIYOYMD = t2.CMG_TEKIYOYMD;

  -- 削除対象レコードの [削除フラグ] を設定
		UPDATE #wt_IMPORT_MIXMGROUP_3
		SET
			SMGM_DELFG = 1,
			SMGM_KOSINYMD = @NowDt,
			SMGM_KOSINTANTO = @Tanto;
			
  -- ＤＢレコードを更新
		INSERT INTO MIXMATCHGRP_MST_now
		SELECT *
		FROM #wt_IMPORT_MIXMGROUP_3;
		
-- [会社コード、店舗コード、ミックスマッチ番号] で予約削除（※ [適用日] 以降）
		WITH
			t1 AS (
				SELECT CMG_KCD, CMG_TENCD, CMG_MMNO, CMG_TEKIYOYMD, CMG_MMNAME, CMG_IMPORTFILE
				FROM #wt_IMPORT_MIXMGROUP_1
				WHERE CMG_YDELKBN = 2
			),
			t2 AS (
				SELECT SMGM_KCD, SMGM_HTCD, SMGM_MMNOBIG, SMGM_TEKIYOYMD
				FROM MIXMATCHGRP_MST_now
				WHERE SMGM_DELFG <> 1
			),
			t3 AS (
				SELECT DISTINCT t1.CMG_KCD, t1.CMG_TENCD, t1.CMG_MMNO, t1.CMG_TEKIYOYMD
				FROM t1
				INNER JOIN t2
				ON t1.CMG_KCD = t2.SMGM_KCD
					AND t1.CMG_TENCD = t2.SMGM_HTCD
					AND t1.CMG_MMNO = t2.SMGM_MMNOBIG
					AND t1.CMG_TEKIYOYMD <= t2.SMGM_TEKIYOYMD
			)
		SELECT t1.CMG_KCD, t1.CMG_TENCD, t1.CMG_MMNO, t1.CMG_TEKIYOYMD, t1.CMG_MMNAME, t1.CMG_IMPORTFILE, t3.CMG_KCD AS CMG_KCD_2
		INTO #wt_IMPORT_MIXMGROUP_4
		FROM t1
		LEFT OUTER JOIN t3
		ON t1.CMG_KCD = t3.CMG_KCD
			AND t1.CMG_TENCD = t3.CMG_TENCD
			AND t1.CMG_MMNO = t3.CMG_MMNO
			AND t1.CMG_TEKIYOYMD = t3.CMG_TEKIYOYMD;
			
  -- 削除対象レコードが存在しない削除設定を取得
		SELECT CMG_KCD, CMG_TENCD, CMG_MMNO, CMG_TEKIYOYMD, CMG_MMNAME,
			RIGHT(CMG_IMPORTFILE, LEN(CMG_IMPORTFILE) - PATINDEX('%|%', CMG_IMPORTFILE)) AS CSV_LINENO
		FROM #wt_IMPORT_MIXMGROUP_4
		WHERE CMG_KCD_2 IS NULL;
		
  -- 削除対象レコードを取得
		WITH
			t1 AS (
				SELECT *
				FROM MIXMATCHGRP_MST_now
				WHERE SMGM_DELFG <> 1
			),
			t2 AS (
				SELECT CMG_KCD, CMG_TENCD, CMG_MMNO, MIN(CMG_TEKIYOYMD) AS CMG_TEKIYOYMD
				FROM #wt_IMPORT_MIXMGROUP_4
				WHERE CMG_KCD_2 IS NOT NULL
				GROUP BY CMG_KCD, CMG_TENCD, CMG_MMNO
			)
		SELECT t1.*
		INTO #wt_IMPORT_MIXMGROUP_5
		FROM t1
		INNER JOIN t2
		ON t1.SMGM_KCD = t2.CMG_KCD
			AND t1.SMGM_HTCD = t2.CMG_TENCD
			AND t1.SMGM_MMNOBIG = t2.CMG_MMNO
			AND t1.SMGM_TEKIYOYMD >= t2.CMG_TEKIYOYMD;
			
  -- 削除対象レコードの [削除フラグ] を設定
		UPDATE #wt_IMPORT_MIXMGROUP_5
		SET
			SMGM_DELFG = 1,
			SMGM_KOSINYMD = @NowDt,
			SMGM_KOSINTANTO = @Tanto;
			
  -- ＤＢレコードを更新
		INSERT INTO MIXMATCHGRP_MST_now
		SELECT *
		FROM #wt_IMPORT_MIXMGROUP_5;
		
-- [会社コード、店舗コード、ミックスマッチ番号] でマスタ削除（※ [適用日] 時点）
		WITH
			t1 AS (
				SELECT CMG_KCD, CMG_TENCD, CMG_MMNO, CMG_TEKIYOYMD, CMG_MMNAME, CMG_IMPORTFILE
				FROM #wt_IMPORT_MIXMGROUP_1
				WHERE CMG_MDELKBN = 1
			)
		SELECT t1.CMG_KCD, t1.CMG_TENCD, t1.CMG_MMNO, t1.CMG_TEKIYOYMD, t1.CMG_MMNAME, t1.CMG_IMPORTFILE, t2.SMGM_TEKIYOYMD
		INTO #wt_IMPORT_MIXMGROUP_101
		FROM t1
		LEFT OUTER JOIN vi_MIXMATCHGRP_MST t2
		ON t1.CMG_KCD = t2.SMGM_KCD
			AND t1.CMG_TENCD = t2.SMGM_HTCD
			AND t1.CMG_MMNO = t2.SMGM_MMNOBIG
			AND t1.CMG_TEKIYOYMD >= t2.SMGM_TEKIYOYMD
			AND t1.CMG_TEKIYOYMD <= t2.SMGM_TEKIYOYMD_END;

  -- 削除対象レコードが存在しない削除設定を取得
		SELECT CMG_KCD, CMG_TENCD, CMG_MMNO, CMG_TEKIYOYMD, CMG_MMNAME,
			RIGHT(CMG_IMPORTFILE, LEN(CMG_IMPORTFILE) - PATINDEX('%|%', CMG_IMPORTFILE)) AS CSV_LINENO
		FROM #wt_IMPORT_MIXMGROUP_101
		WHERE SMGM_TEKIYOYMD IS NULL;
		
  -- 削除対象レコードを取得
		WITH
			t1 AS (
				SELECT *
				FROM MIXMATCHGRP_MST_now
				WHERE SMGM_DELFG = 0
			)
		SELECT t1.*, t2.CMG_TEKIYOYMD
		INTO #wt_IMPORT_MIXMGROUP_102
		FROM t1
		INNER JOIN #wt_IMPORT_MIXMGROUP_101 t2
		ON t1.SMGM_KCD = t2.CMG_KCD
			AND t1.SMGM_HTCD = t2.CMG_TENCD
			AND t1.SMGM_MMNOBIG = t2.CMG_MMNO
			AND t1.SMGM_TEKIYOYMD = t2.SMGM_TEKIYOYMD;

  -- 削除対象レコードを編集
		UPDATE #wt_IMPORT_MIXMGROUP_102
		SET
			SMGM_TEKIYOYMD = CMG_TEKIYOYMD,
			SMGM_UPDATECNT = 0,
			SMGM_DELFG = 2,
			SMGM_INYMD = @NowDt,
			SMGM_INTANTO = @Tanto,
			SMGM_KOSINYMD = @NowDt,
			SMGM_KOSINTANTO = @Tanto
		WHERE SMGM_TEKIYOYMD < CMG_TEKIYOYMD;

		UPDATE #wt_IMPORT_MIXMGROUP_102
		SET
			SMGM_DELFG = 2,
			SMGM_KOSINYMD = @NowDt,
			SMGM_KOSINTANTO = @Tanto
		WHERE SMGM_TEKIYOYMD = CMG_TEKIYOYMD;
			
  -- ＤＢレコードを更新
		INSERT INTO MIXMATCHGRP_MST_now
		SELECT SMGM_KCD,
			SMGM_HTCD,
			SMGM_MMNO,
			SMGM_TEKIYOYMD,
			SMGM_UPDATECNT,
			SMGM_HANEIYMD,
			SMGM_MMNOBIG,
			SMGM_MMNAME,
			SMGM_MMSTR,
			SMGM_MMEND,
			SMGM_SETKOSU1,
			SMGM_SETKINGAKU1,
			SMGM_SETKOSU2,
			SMGM_SETKINGAKU2,
			SMGM_YDELKBN,
			SMGM_MDELKBN,
			SMGM_YOBI1,
			SMGM_YOBI2,
			SMGM_YOBI3,
			SMGM_YOBI4,
			SMGM_YOBI5,
			SMGM_YOBI6,
			SMGM_YOBI7,
			SMGM_YOBI8,
			SMGM_YOBI9,
			SMGM_IMPORTYMD,
			SMGM_IMPORTFILE,
			SMGM_DELFG,
			SMGM_INYMD,
			SMGM_INTANTO,
			SMGM_KOSINYMD,
			SMGM_KOSINTANTO
		FROM #wt_IMPORT_MIXMGROUP_102;

-- ミックスマッチ番号マスタ更新
		WITH
			t1 AS (
				SELECT b.MMNO, MAX(a.CMG_MMEND) AS CMG_MMEND
				FROM #wt_IMPORT_MIXMGROUP_1 a
				INNER JOIN MIXMATCH_MMNO b
				ON a.CMG_MMNO = b.CSVMMNO
					AND a.CMG_MMEND > b.ENDYMD
				GROUP BY b.MMNO
				/*
				WHERE a.CMG_YDELKBN = 0
					AND a.CMG_MDELKBN = 0
				*/
			)
		UPDATE MIXMATCH_MMNO
		SET ENDYMD = t1.CMG_MMEND
		FROM MIXMATCH_MMNO a
		INNER JOIN t1
			ON a.MMNO = t1.MMNO;

		WITH
			t1 AS (
				SELECT CMG_MMNO, MAX(CMG_MMEND) AS CMG_MMEND
				FROM #wt_IMPORT_MIXMGROUP_1
				GROUP BY CMG_MMNO
			),
			t2 AS (
				SELECT t1.CMG_MMNO, t1.CMG_MMEND,
					ROW_NUMBER() OVER(ORDER BY t1.CMG_MMNO ASC) AS row_num
				FROM t1
				LEFT OUTER JOIN MIXMATCH_MMNO t2
					ON t1.CMG_MMNO = t2.CSVMMNO
				WHERE t2.MMNO IS NULL
			),
			t3 AS (
				SELECT *,
					ROW_NUMBER() OVER(ORDER BY ENDYMD ASC) AS row_num
				FROM MIXMATCH_MMNO
				WHERE ENDYMD IS NULL OR ENDYMD < DATEADD(day, -7, CAST(GETDATE() AS date))
			),
			t4 AS (
				SELECT t2.CMG_MMNO, t2.CMG_MMEND, t3.MMNO
				FROM t2
				INNER JOIN t3
				ON t2.row_num = t3.row_num
			)
		UPDATE MIXMATCH_MMNO
		SET CSVMMNO = t4.CMG_MMNO,
			ENDYMD = t4.CMG_MMEND
		FROM MIXMATCH_MMNO t5
		INNER JOIN t4
		ON t5.MMNO = t4.MMNO;

-- ミックスマッチ商品削除（MMNO が、新しいミックスグループに再利用されたもの）
		WITH
			t1 AS (
				SELECT *
				FROM MIXMATCH_MST_now
				WHERE SMIM_DELFG = 0
			),
			t2 AS (
				SELECT DISTINCT CSVMMNO
				FROM MIXMATCH_MMNO
			)
		SELECT t1.*
		INTO #wt_IMPORT_MIXMGROUP_201
		FROM t1
		LEFT OUTER JOIN t2
		ON t1.SMIM_MMNOBIG = t2.CSVMMNO
		WHERE t2.CSVMMNO IS NULL;

		UPDATE #wt_IMPORT_MIXMGROUP_201
		SET
			SMIM_DELFG = 1,
			SMIM_KOSINYMD = @NowDt,
			SMIM_KOSINTANTO = @Tanto;

		INSERT INTO MIXMATCH_MST_now
		SELECT *
		FROM #wt_IMPORT_MIXMGROUP_201;

-- レコード登録
		SELECT t1.*, t2.*
		INTO #wt_IMPORT_MIXMGROUP_6
		FROM #wt_IMPORT_MIXMGROUP_1 t1
		LEFT OUTER JOIN MIXMATCHGRP_MST_now t2
		ON t1.CMG_KCD = t2.SMGM_KCD
			AND t1.CMG_TENCD = t2.SMGM_HTCD
			AND t1.CMG_MMNO = t2.SMGM_MMNOBIG
			AND t1.CMG_TEKIYOYMD = t2.SMGM_TEKIYOYMD;

  -- レコード更新
    -- 予約・マスタ 削除レコード
		INSERT INTO MIXMATCHGRP_MST_now
		SELECT
			SMGM_KCD,
			SMGM_HTCD,
			SMGM_MMNO,
			SMGM_TEKIYOYMD,
			SMGM_UPDATECNT,
			SMGM_HANEIYMD,
			SMGM_MMNOBIG,
			SMGM_MMNAME,
			SMGM_MMSTR,
			SMGM_MMEND,
			SMGM_SETKOSU1,
			SMGM_SETKINGAKU1,
			SMGM_SETKOSU2,
			SMGM_SETKINGAKU2,
			CMG_YDELKBN,
			CMG_MDELKBN,
			SMGM_YOBI1,
			SMGM_YOBI2,
			SMGM_YOBI3,
			SMGM_YOBI4,
			SMGM_YOBI5,
			SMGM_YOBI6,
			SMGM_YOBI7,
			SMGM_YOBI8,
			SMGM_YOBI9,
			CMG_IMPORTYMD,
			CMG_IMPORTFILE,
			SMGM_DELFG,
			SMGM_INYMD,
			SMGM_INTANTO,
			@NowDt,
			@Tanto
		FROM #wt_IMPORT_MIXMGROUP_6
		WHERE SMGM_KCD IS NOT NULL
			AND CMG_YDELKBN + CMG_MDELKBN > 0;
			
    -- 通常レコード
		INSERT INTO MIXMATCHGRP_MST_now
		SELECT
			SMGM_KCD,
			SMGM_HTCD,
			SMGM_MMNO,
			SMGM_TEKIYOYMD,
			SMGM_UPDATECNT,
			CMG_HANEIYMD,
			SMGM_MMNOBIG,
			ISNULL(CMG_MMNAME,' '),
			ISNULL(CMG_MMSTR, CAST('1753-1-1' AS datetime)),
			ISNULL(CMG_MMEND, CAST('1753-1-1' AS datetime)),
			CMG_SETKOSU1,
			CMG_SETKINGAKU1,
			CMG_SETKOSU2,
			CMG_SETKINGAKU2,
			CMG_YDELKBN,
			CMG_MDELKBN,
			CMG_YOBI1,
			CMG_YOBI2,
			CMG_YOBI3,
			CMG_YOBI4,
			CMG_YOBI5,
			CMG_YOBI6,
			CMG_YOBI7,
			CMG_YOBI8,
			CMG_YOBI9,
			CMG_IMPORTYMD,
			CMG_IMPORTFILE,
			0,
			SMGM_INYMD,
			SMGM_INTANTO,
			@NowDt,
			@Tanto
		FROM #wt_IMPORT_MIXMGROUP_6
		WHERE SMGM_KCD IS NOT NULL
			AND CMG_YDELKBN = 0 AND CMG_MDELKBN = 0;

  -- レコード追加
    -- 予約・マスタ 削除レコード
		WITH
			t1 AS (
				SELECT *
				FROM #wt_IMPORT_MIXMGROUP_6
				WHERE SMGM_KCD IS NULL
					AND CMG_YDELKBN + CMG_MDELKBN > 0
			)
		INSERT INTO MIXMATCHGRP_MST_now
		SELECT
			t1.CMG_KCD,
			t1.CMG_TENCD,
			t2.MMNO,
			t1.CMG_TEKIYOYMD,
			0,
			t1.CMG_HANEIYMD,
			t1.CMG_MMNO,
			ISNULL(t1.CMG_MMNAME,' '),
			ISNULL(t1.CMG_MMSTR, CAST('1753-1-1' AS datetime)),
			ISNULL(t1.CMG_MMEND, CAST('1753-1-1' AS datetime)),
			t1.CMG_SETKOSU1,
			t1.CMG_SETKINGAKU1,
			t1.CMG_SETKOSU2,
			t1.CMG_SETKINGAKU2,
			t1.CMG_YDELKBN,
			t1.CMG_MDELKBN,
			t1.CMG_YOBI1,
			t1.CMG_YOBI2,
			t1.CMG_YOBI3,
			t1.CMG_YOBI4,
			t1.CMG_YOBI5,
			t1.CMG_YOBI6,
			t1.CMG_YOBI7,
			t1.CMG_YOBI8,
			t1.CMG_YOBI9,
			t1.CMG_IMPORTYMD,
			t1.CMG_IMPORTFILE,
			1,
			@NowDt,
			@Tanto,
			@NowDt,
			@Tanto
		FROM t1
		LEFT OUTER JOIN MIXMATCH_MMNO t2
		ON t1.CMG_MMNO = t2.CSVMMNO;

    -- 通常レコード
		WITH
			t1 AS (
				SELECT *
				FROM #wt_IMPORT_MIXMGROUP_6
				WHERE SMGM_KCD IS NULL
					AND CMG_YDELKBN = 0 AND CMG_MDELKBN = 0
			)
		INSERT INTO MIXMATCHGRP_MST_now
		SELECT
			t1.CMG_KCD,
			t1.CMG_TENCD,
			t2.MMNO,
			t1.CMG_TEKIYOYMD,
			0,
			t1.CMG_HANEIYMD,
			t1.CMG_MMNO,
			ISNULL(t1.CMG_MMNAME,' '),
			ISNULL(t1.CMG_MMSTR, CAST('1753-1-1' AS datetime)),
			ISNULL(t1.CMG_MMEND, CAST('1753-1-1' AS datetime)),
			t1.CMG_SETKOSU1,
			t1.CMG_SETKINGAKU1,
			t1.CMG_SETKOSU2,
			t1.CMG_SETKINGAKU2,
			t1.CMG_YDELKBN,
			t1.CMG_MDELKBN,
			t1.CMG_YOBI1,
			t1.CMG_YOBI2,
			t1.CMG_YOBI3,
			t1.CMG_YOBI4,
			t1.CMG_YOBI5,
			t1.CMG_YOBI6,
			t1.CMG_YOBI7,
			t1.CMG_YOBI8,
			t1.CMG_YOBI9,
			t1.CMG_IMPORTYMD,
			t1.CMG_IMPORTFILE,
			0,
			@NowDt,
			@Tanto,
			@NowDt,
			@Tanto
		FROM t1
		LEFT OUTER JOIN MIXMATCH_MMNO t2
		ON t1.CMG_MMNO = t2.CSVMMNO;

		IF @TranCnt = 0
            COMMIT TRANSACTION;
	END TRY
	BEGIN CATCH
		IF @TranCnt = 0  
            ROLLBACK TRANSACTION;
		ELSE IF XACT_STATE() <> -1
			ROLLBACK TRANSACTION svpt_IMPORT_MIXMGROUP_1;

		SET @ErrMessage = ERROR_MESSAGE();
        SET @ErrSeverity = ERROR_SEVERITY();
        SET @ErrState = ERROR_STATE();
  
        RAISERROR (@ErrMessage, @ErrSeverity, @ErrState );
	END CATCH
END
GO
